\documentclass{article}

\usepackage{arxiv}
\usepackage{bussproofs}
\usepackage{amsmath}
\usepackage{color}
\usepackage{listings}

\definecolor{dkgreen}{rgb}{0,0.6,0}
\definecolor{gray}{rgb}{0.5,0.5,0.5}
\definecolor{mauve}{rgb}{0.58,0,0.82}

\lstset{frame=tb,
  language=Haskell,
  aboveskip=3mm,
  belowskip=3mm,
  showstringspaces=false,
  columns=flexible,
  basicstyle={\small\ttfamily},
  numbers=none,
  numberstyle=\tiny\color{gray},
  keywordstyle=\color{blue},
  commentstyle=\color{dkgreen},
  stringstyle=\color{mauve},
  breaklines=true,
  breakatwhitespace=true,
  frame=none,
  tabsize=3
}

\title{Tutorial on implementing Hoare logic for imperative programs in Haskell}

\author{
  Boro Sitnikovski \\
  Skopje, North Macedonia \\
  \texttt{buritomath@gmail.com} \\
}

\begin{document}
\maketitle

\begin{abstract}
Using the programming language Haskell, we introduce an implementation for a simple imperative language that can evaluate arithmetic and boolean expressions; step by step, we will expand the implementation starting from arithmetic expressions, to boolean expressions and then to imperative programs. As we expand the language, we will show several evaluation strategies, considering the normalization property and how it affects an implementation. Finally, we will provide a bottom-up implementation of Hoare's logic which will allow us to deduce facts about programs without the need for a full evaluation.
\end{abstract}

\keywords{Imperative languages \and Functional languages \and Hoare logic \and Formal verification \and Haskell}

\section{Introduction}

Nowadays, imperative programming languages run the world, to list a few popular ones: C, Python, JavaScript, PHP. Code is usually written in these languages by using an imperative style; that is, the computer is being told ("commanded") what to do and how to do it specifically. The mathematical language is very unlike this, it is more declarative rather than imperative, that is, it doesn't care about the how.

Since a programmer has to specify the exact "how", it is easy to make a programming error. In addition, given the popularity of these programming languages, there are many bugs in software applications that are programmed in them. This motivates for a way to formally verify certain properties about programs written in these languages. Hoare logic is one way to mechanically reason about computer programs.

We will provide an implementation of an imperative language together with a toy implementation of Hoare logic that will allow us to reason about programs in this language. Our implementation of Hoare's logic will be bottom-up, in the sense that we will build the proofs from the ground up, in contrast to e.g. programming languages such as Dafny\cite{b1}, that build proofs from top to bottom. That is, in Dafny, the user provides a proposition and Dafny will derive the proof, automatically, using the automated theorem prover Z3\cite{b2}.

The programming language that we will implement already has an implementation in Coq\cite{b3}. However, implementing a language in Haskell is more concerned about playing at the value level (and to some extent at the type level), whereas in a dependently-typed language the focus is at the type level. Specifically for Hoare's logic, in Haskell we cannot use any of the meta language's constructs to do mathematical proofs, so we have to take care of these algorithms ourselves.

Haskell is not a strongly normalizing language, which means that not every evaluation necessarily terminates. However, in dependently-typed languages, such as Coq\cite{b4}, the evaluation of proof terms (e.g. the type checker) is strongly normalizing, and this is what allows us to express mathematical proofs.

There are good introductory books on dependent types\cite{b3}. In some books, there is a gentler approach that might be handy for newcomers\cite{b5}. There are good introductions to Haskell as well\cite{b6}.

\section{Numbers language}

We provide the syntax, evaluation rules, and the implementation in Haskell of a simple language that is strongly normalizing.

\subsection{Arithmetic}

We start by introducing a language that can do arithmetic. The syntax of the language expressed in BNF (Backus-Naur Form) is as follows:

\begin{lstlisting}
digit  ::= "0" | "1" | ... | "8" | "9"
aexp   ::= aterm | aterm relop aterm
number ::= - digit* | digit*
relop  ::= "+" | "-" | "*"
aterm  ::= aexp | number | var
var    ::= A | B | C ... | Y | Z
\end{lstlisting}

Followed by a direct translation to Haskell code:

\begin{lstlisting}
data Aexp =
  ANum Integer
  | AId Char
  | APlus Aexp Aexp
  | AMinus Aexp Aexp
  | AMult Aexp Aexp
\end{lstlisting}

We show the evaluation rules for optimizing an expression. Here, "optimization" is just another evaluation strategy.

\[
\AxiomC{}
\RightLabel{(A-Opt-Plus)}
\UnaryInfC{$\texttt{APlus} \ \$a_1 \ \$a_2 \hookrightarrow \$(a_1 + a_2)$}
\DisplayProof
\quad
\AxiomC{}
\RightLabel{(A-Opt-Minus)}
\UnaryInfC{$\texttt{AMinus} \ \$a_1 \ \$a_2 \hookrightarrow \$(a_1 - a_2)$}
\DisplayProof
\quad
\]
\hfill
\[
\AxiomC{}
\RightLabel{(A-Opt-Mult)}
\UnaryInfC{$\texttt{AMult} \ \$a_1 \ \$a_2 \hookrightarrow \$(a_1 \cdot a_2)$}
\DisplayProof
\]

That is, we lift the values from our language to the meta language (Haskell), where the symbol $\$$ represents the value constructor \texttt{ANum}, and the symbol $\hookrightarrow$ represents the optimization function.

The same rules represented in Haskell:

\begin{lstlisting}
aoptimize :: Aexp -> Aexp
aoptimize (APlus (ANum a1) (ANum a2)) = ANum (a1 + a2)
aoptimize (AMinus (ANum a1) (ANum a2)) = ANum (a1 - a2)
aoptimize (AMult (ANum a1) (ANum a2)) = ANum (a1 * a2)
aoptimize x = x
\end{lstlisting}

\subsection{Boolean}

Next, we introduce a language that can handle boolean expressions. The syntax of the language expressed in BNF is:

\begin{lstlisting}
bexp   ::= bterm | bterm brelop bterm | aterm arelop aterm | unop bterm
arelop ::= "==" | "<"
brelop ::= "&&"
bterm  ::= "T" | "F"
unop   ::= "!"
\end{lstlisting}

The same syntax is represented with Haskell code:

\begin{lstlisting}
data Bexp =
  BTrue
  | BFalse
  | BEq Aexp Aexp
  | BLe Aexp Aexp
  | BNot Bexp
  | BAnd Bexp Bexp
\end{lstlisting}

Similarly to \texttt{aoptimize}, we list the evaluation rules for optimizing a boolean expression:

\[
\AxiomC{$a_1 = a_2$}
\RightLabel{(B-Opt-EqNumTrue)}
\UnaryInfC{$\texttt{BEq} \ \$a_1 \ \$a_2 \hookrightarrow \texttt{BTrue}$}
\DisplayProof
\quad
\AxiomC{$a_1 \neq a_2$}
\RightLabel{(B-Opt-EqNumFalse)}
\UnaryInfC{$\texttt{BEq} \ \$a_1 \ \$a_2 \hookrightarrow \texttt{BFalse}$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$v_1 = v_2$}
\RightLabel{(B-Opt-EqIdTrue)}
\UnaryInfC{$\texttt{BEq} \ \#v_1 \ \#v_2 \hookrightarrow \texttt{BTrue}$}
\DisplayProof
\quad
\AxiomC{$v_1 \neq v_2$}
\RightLabel{(B-Opt-EqIdFalse)}
\UnaryInfC{$\texttt{BEq} \ \#v_1 \ \#v_2 \hookrightarrow \texttt{BEq} \ \#v_1 \ \#v_2$}
\DisplayProof
\]
\hfill
\[
\AxiomC{}
\RightLabel{(B-Opt-NegTrue)}
\UnaryInfC{$ \texttt{BNot} \ \texttt{BTrue} \hookrightarrow \texttt{BFalse}$}
\DisplayProof
\quad
\AxiomC{}
\RightLabel{(B-Opt-NegFalse)}
\UnaryInfC{$ \texttt{BNot} \ \texttt{BFalse} \hookrightarrow \texttt{BTrue}$}
\DisplayProof
\]
\hfill
\[
\AxiomC{}
\RightLabel{(B-Opt-AndFalse)}
\UnaryInfC{$ \texttt{BAnd BFalse} \ x \hookrightarrow \texttt{BFalse} $}
\DisplayProof
\quad
\AxiomC{}
\RightLabel{(B-Opt-AndTrue)}
\UnaryInfC{$ \texttt{BAnd BTrue} \ x \hookrightarrow x $}
\DisplayProof
\]

Where the $\#$ symbol represents a variable (the value constructor \texttt{AId}).

These rules translate to the following implementation in Haskell:

\begin{lstlisting}
boptimize :: Bexp -> Bexp
boptimize (BEq (ANum a1) (ANum a2)) = if a1 == a2 then BTrue else BFalse
boptimize (BEq (AId v1) (AId v2)) = if v1 == v2 then BTrue else BEq (AId v1) (AId v2)
boptimize (BNot BTrue) = BFalse
boptimize (BNot BFalse) = BTrue
boptimize (BAnd BFalse _) = BFalse
boptimize (BAnd BTrue b2) = b2
boptimize x = x
\end{lstlisting}

\subsection{Evaluation}

The optimization functions \texttt{aoptimize} and \texttt{boptimize} represent a simple single-step evaluation. Next, we introduce a way to do a full evaluation of arithmetic and boolean expressions.

To support variables, we introduce the notion of a context; one implementation in Haskell is simply a mapping from characters to numbers.

\begin{lstlisting}
type Context = M.Map Char Integer
\end{lstlisting}

We list the evaluation rules for arithmetic expressions:

\[
\AxiomC{$(\#v, \$v') \in ctx$}
\RightLabel{(A-Eval-Id)}
\UnaryInfC{$\#v \underset{ctx}{\to} \$v'$}
\DisplayProof
\quad
\AxiomC{}
\RightLabel{(A-Eval-Num)}
\UnaryInfC{$\$n \underset{ctx}{\to} n$}
\DisplayProof
\quad
\AxiomC{$a_1 \underset{ctx}{\to} a_1' , a_2 \underset{ctx}{\to} a_2'$}
\RightLabel{(A-Eval-Plus)}
\UnaryInfC{$\texttt{APlus} \ a_1 \ a_2 \underset{ctx}{\to} a_1' + a_2'$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$a_1 \underset{ctx}{\to} a_1' , a_2 \underset{ctx}{\to} a_2'$}
\RightLabel{(A-Eval-Minus)}
\UnaryInfC{$\texttt{AMinus} \ a_1 \ a_2 \underset{ctx}{\to} a_1' - a_2'$}
\DisplayProof
\quad
\quad
\AxiomC{$a_1 \underset{ctx}{\to} a_1' , a_2 \underset{ctx}{\to} a_2'$}
\RightLabel{(A-Eval-Mult)}
\UnaryInfC{$\texttt{AMult} \ a_1 \ a_2 \underset{ctx}{\to} a_1' \cdot a_2'$}
\DisplayProof
\]

The arrow symbol $\underset{ctx}{\to}$ in the rules represents the actual evaluation of arithmetic expressions under context $ctx$.

Follows the implementation of these rules in Haskell:

\begin{lstlisting}
aeval :: Context -> Aexp -> Integer
aeval ctx (AId v) = ctx M.! v -- element may not exist
aeval ctx (ANum n) = n
aeval ctx (APlus a1 a2) = aeval ctx a1 + aeval ctx a2
aeval ctx (AMinus a1 a2) = aeval ctx a1 - aeval ctx a2
aeval ctx (AMult a1 a2) = aeval ctx a1 * aeval ctx a2
\end{lstlisting}

Finally, the evaluation rules for boolean expressions:

\[
\AxiomC{}
\RightLabel{(B-Eval-True)}
\UnaryInfC{$\texttt{BTrue} \underset{ctx}{\Rightarrow} \texttt{True}$}
\DisplayProof
\quad
\AxiomC{}
\RightLabel{(B-Eval-False)}
\UnaryInfC{$\texttt{BFalse} \underset{ctx}{\Rightarrow} \texttt{False}$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$a_1 \underset{ctx}{\to} a_1' , a_2 \underset{ctx}{\to} a_2'$}
\RightLabel{(B-Eval-Eq)}
\UnaryInfC{$\texttt{BEq} \ a_1 \ a_2 \underset{ctx}{\Rightarrow} a_1' = a_2'$}
\DisplayProof
\quad
\AxiomC{$a_1 \underset{ctx}{\to} a_1' , a_2 \underset{ctx}{\to} a_2'$}
\RightLabel{(B-Eval-Le)}
\UnaryInfC{$\texttt{BLe} \ a_1 \ a_2 \underset{ctx}{\Rightarrow} a_1' \le a_2'$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$b \underset{ctx}{\Rightarrow} b'$}
\RightLabel{(B-Eval-Not)}
\UnaryInfC{$\texttt{BNot} \ b' \underset{ctx}{\Rightarrow} \neg b'$}
\DisplayProof
\quad
\AxiomC{$b_1 \underset{ctx}{\Rightarrow} b_1' , b_2 \underset{ctx}{\Rightarrow} b_2'$}
\RightLabel{(B-Eval-And)}
\UnaryInfC{$\texttt{BAnd} \ b_1 \ b_2 \underset{ctx}{\Rightarrow} b_1' \land b_2'$}
\DisplayProof
\]

Similarly, the double right arrow symbol $\underset{ctx}{\Rightarrow}$ in the rules represents the actual evaluation of boolean expressions under context $ctx$.

We provide the implementation of these rules in Haskell:

\begin{lstlisting}
beval :: Context -> Bexp -> Bool
beval ctx BTrue = True
beval ctx BFalse = False
beval ctx (BEq a1 a2) = aeval ctx a1 == aeval ctx a2
beval ctx (BLe a1 a2) = aeval ctx a1 <= aeval ctx a2
beval ctx (BNot b1) = not (beval ctx b1)
beval ctx (BAnd b1 b2) = beval ctx b1 && beval ctx b2
\end{lstlisting}

For example, the single-step optimization functions can be used as following:

\begin{lstlisting}
> let e = BNot BTrue in "Optimize: " ++ show e ++ " = " ++ show (boptimize e)
Optimize: ! (TRUE) = FALSE
> let e = APlus (ANum 2) (ANum 5) in "Optimize: " ++ show e ++ " = " ++ show (aoptimize e)
Optimize: 2 + 5 = 7
\end{lstlisting}

However, since the single-step evaluation strategy does not rely on any context, it will not be able to deduce much about expressions that involve variables, e.g. \texttt{X + 5}, while \texttt{aeval} can substitute for this variable, given a context:

\begin{lstlisting}
> let e = APlus (AId 'X') (ANum 5) in "Optimize: " ++ show e ++ " = " ++ show (aoptimize e)
Optimize: X + 5 = X + 5
> let e = APlus (AId 'X') (ANum 5) in show e ++ " = " ++ show (aeval (M.fromList [('X', 5)]) e)
X + 5 = 10
> let e = BEq (AId 'X') (ANum 5) in show e ++ " = " ++ show (beval (M.fromList [('X', 5)]) e)
X == 5 = True
\end{lstlisting}

\section{Imperative language}

We proceed with providing the syntax, evaluation rules, and the implementation in Haskell of an imperative language.

\subsection{Commands}

We show the data type of the imperative language expressed in Haskell:

\begin{lstlisting}
data Command =
  CSkip
  | CAssign Char Aexp
  | CSequence Command Command
  | CIfElse Bexp Command Command
  | CWhile Bexp Command
  | CAssert Bexp Command Bexp
\end{lstlisting}

That is, the language contains the minimum set of commands which make an imperative language:

\begin{itemize}
\item \texttt{CSkip} is the the no operation command - the empty statement.
\item \texttt{CAssign} will assign a value to a variable in a context.
\item \texttt{CSequence} will join two commands, which allows for the evaluation of commands in sequence.
\item \texttt{CIfElse} accepts a boolean and depending on its value either executes one command, or another.
\item \texttt{CWhile} accepts a boolean and keeps executing a command as long as the boolean is true.
\item \texttt{CAssert} accepts a precondition, a command, and a postcondition. The evaluation will be successful if the precondition and the postcondition are satisfied before and after executing the command, respectively.
\end{itemize}

We show the evaluation rules for this language:

\[
\AxiomC{}
\RightLabel{(C-Eval-Skip)}
\UnaryInfC{$\texttt{CSkip} \underset{ctx}{\mapsto} ctx$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$v \underset{ctx}{\to} v'$}
\RightLabel{(C-Eval-Assign)}
\UnaryInfC{$\texttt{CAssign} \ c \ v \underset{ctx}{\mapsto} ctx \cup (c, v')$}
\DisplayProof
\quad
\AxiomC{$c_1 \underset{ctx}{\mapsto} ctx', c_2 \underset{ctx'}{\mapsto} ctx''$}
\RightLabel{(C-Eval-Sequence)}
\UnaryInfC{$\texttt{CSequence} \ c_1 \ c_2 \underset{ctx}{\mapsto} ctx''$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$b \underset{ctx}{\Rightarrow} \texttt{True}, c_1 \underset{ctx}{\mapsto} ctx'$}
\RightLabel{(C-Eval-IfTrue)}
\UnaryInfC{$\texttt{CIfElse} \ b \ c_1 \ c_2 \underset{ctx}{\mapsto} ctx'$}
\DisplayProof
\quad
\AxiomC{$b \underset{ctx}{\Rightarrow} \texttt{False}, c_2 \underset{ctx}{\mapsto} ctx'$}
\RightLabel{(C-Eval-IfFalse)}
\UnaryInfC{$\texttt{CIfElse} \ b \ c_1 \ c_2 \underset{ctx}{\mapsto} ctx'$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$b \underset{ctx}{\Rightarrow} \texttt{True}, c \underset{ctx}{\mapsto} ctx', \texttt{CWhile} \ b \ c \underset{ctx'}{\mapsto} ctx''$}
\RightLabel{(C-Eval-WhileTrue)}
\UnaryInfC{$\texttt{CWhile} \ b \ c \underset{ctx}{\mapsto} ctx''$}
\DisplayProof
\quad
\AxiomC{$b \underset{ctx}{\Rightarrow} \texttt{False}$}
\RightLabel{(C-Eval-WhileFalse)}
\UnaryInfC{$\texttt{CWhile} \ b \ c \underset{ctx}{\mapsto} ctx$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$b_1 \underset{ctx}{\Rightarrow} \texttt{True}, c \underset{ctx}{\mapsto} ctx', b_2 \underset{ctx'}{\Rightarrow} \texttt{True}$}
\RightLabel{(C-Eval-Assert)}
\UnaryInfC{$\texttt{CAssert} \ b_1 \ c \ b_2$}
\DisplayProof
\]

The map arrow symbol $\underset{ctx}{\mapsto}$ in the rules represents the actual evaluation of a command under context $ctx$.

In Coq, each implementation of the rules would be represented at the type level. However, since we're working at the value level in Haskell, we will rely on the \texttt{Either} data type to distinguish between provable and not provable terms.

\begin{lstlisting}
eval :: Context -> Command -> Either String Context
eval ctx CSkip = Right ctx
eval ctx (CAssign c v) = Right $ M.insert c (aeval ctx v) ctx
eval ctx (CSequence c1 c2) = let ctx' = eval ctx c1 in whenRight ctx' (\ctx'' -> eval ctx'' c2)
eval ctx (CIfElse b c1 c2) = eval ctx $ if beval ctx b then c1 else c2
eval ctx (CWhile b c) =
  if beval ctx b
  then let ctx' = eval ctx c in whenRight ctx' (\ctx'' -> eval ctx'' (CWhile b c))
  else Right ctx
eval ctx (CAssert b1 c b2) =
  if beval ctx b1
  then whenRight (eval ctx c)
       (\ctx' -> if beval ctx' b2
                  then Right ctx'
                  else Left "Post-condition does not match!")
  else Left "Pre-condition does not match!"
\end{lstlisting}

Note that this language is not strongly normalizing; consider the evaluation of \texttt{CWhile BTrue CSkip}.

As an example, the factorial program (sequence of commands) can be represented with the pseudo-code:

\begin{lstlisting}
Z := X
Y := 1
while (~Z = 0)
  Y := Y * Z
  Z := Z - 1
\end{lstlisting}

That is, this program will calculate \texttt{Y := X!}. Here's an implementation of it in our language:

\begin{lstlisting}
> :{
| fact_X =
|   let l1 = CAssign 'Z' (AId 'X')
|       l2 = CAssign 'Y' (ANum 1)
|       l3 = CWhile (BNot (BEq (AId 'Z') (ANum 0))) (CSequence l4 l5)
|       l4 = CAssign 'Y' (AMult (AId 'Y') (AId 'Z'))
|       l5 = CAssign 'Z' (AMinus (AId 'Z') (ANum 1))
|   in CSequence l1 (CSequence l2 l3)
| :}
> eval (M.fromList [('X', 5)]) fact_X
Right (fromList [('X',5),('Y',120),('Z',0)])
> let e = CAssert (BEq (ANum 5) (AId 'X')) factX (BEq (ANum 120) (AId 'Y')) in "Assert {X=5} factX {Y=120}: " ++ show (eval (M.fromList [('X', 5)]) e)
Assert {X=5} factX {Y=120}: Right (fromList [('X',5),('Y',120),('Z',0)])
> let e = CAssert (BEq (ANum 4) (AId 'X')) factX (BEq (ANum 120) (AId 'Y')) in "Assert {X=4} factX {Y=120}: " ++ show (eval (M.fromList [('X', 5)]) e)
Assert {X=4} factX {Y=120}: Left "Pre-condition does not match!"
\end{lstlisting}

\section{Hoare logic}

In the previous chapter, we implemented assertions (\texttt{CAssert}) at the run-time (\texttt{eval}) level. The biggest disadvantage of that is we have to do a full evaluation to deduce some facts about programs; considering the assertion example of the \texttt{factX} program, it has to actually evaluate the factorial to conclude something. This motivates the need for an additional evaluation strategy that will allow us to deduce facts about programs without doing a full evaluation.

Some programming languages, like Python, don't have a compile step and the \texttt{eval} function we provided is kind of equivalent to evaluating programs in Python. But some programming languages do have a compile step, like C or Haskell, and this compilation step can be beneficial in that it can do additional different checks, e.g., type checks. That's what we'll do here - implement a "compile"-time check (just another evaluation strategy) using some of the rules in Hoare's logic, and this check can be used to check the validity of a program, before fully evaluating it.

We list the rules of Hoare logic, some of which are outlined in the original paper\cite{b7}.

\[
\AxiomC{}
\RightLabel{(H-Skip)}
\UnaryInfC{$\{P\}\texttt{skip}\{P\}$}
\DisplayProof
\quad
\AxiomC{}
\RightLabel{(H-Assign)}
\UnaryInfC{$\{P[E/x]\}x:=E\{P\}$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$P_1 \to P_2 , \{P_2\}S\{Q_2\} , Q_2 \to Q_1$}
\RightLabel{(H-Consequence)}
\UnaryInfC{$\{P_1\} S \{Q_1\}$}
\DisplayProof
\quad
\AxiomC{$\{P\}S\{Q\} , \{Q\}T\{R\}$}
\RightLabel{(H-Sequence)}
\UnaryInfC{$\{P\}S;T\{R\}$}
\DisplayProof
\]
\hfill
\[
\AxiomC{$\{B \land P\}S\{Q\} , \{\neg B \land P\}T\{Q\}$}
\RightLabel{(H-Conditional)}
\UnaryInfC{$\{P\}\texttt{if }B\texttt{ then }S\texttt{ else } T\{Q\}$}
\DisplayProof
\quad
\quad
\AxiomC{$\{B \land P\}S\{P\}$}
\RightLabel{(H-While)}
\UnaryInfC{$\{P\}\texttt{while }B\texttt{ do }S\{\neg B \land P\}$}
\DisplayProof
\]

\subsection{Implementation}

We represent the Hoare triple as a product of a command, a precondition (\texttt{Bexp}), and a postcondition (\texttt{Bexp}). In this case, the design decision is that pre/postconditions will be in the language of \texttt{Bexp}, but they can also be in a different language (regardless of that \texttt{Bexp} is used in \texttt{Command}).

\begin{lstlisting}
data HoareTriple = HoareTriple Bexp Command Bexp
\end{lstlisting}

Triples should not be constructed with \texttt{HoareTriple}, rather through the functions/rules that we provide next.

\subsubsection{H-Skip}

The Haskell implementation of the Hoare skip rule can be represented as follows:

\begin{lstlisting}
hoareSkip :: Bexp -> HoareTriple
hoareSkip q = HoareTriple q CSkip q
\end{lstlisting}

Note that the validity of the precondition is not being checked (since that would require evaluating \texttt{Aexp}/\texttt{Bexp} with a context); the proof merely states that assuming some precondition, a command produces some postcondition.

\begin{lstlisting}
> hoareSkip (BEq (ANum 3) (ANum 3))
{3 == 3} ; {3 == 3}
\end{lstlisting}

\subsubsection{H-Assign}

Let \texttt{Q[E/V]} denote the expression \texttt{Q} in which each free occurrence of \texttt{V} is replaced with \texttt{E}. Given an assignment command \texttt{V := E}, it should produce the triple where the precondition is \texttt{Q[E/V]} and the postcondition is \texttt{Q}, for any \texttt{Q}.

\begin{lstlisting}
hoareAssignment :: Char -> Aexp -> Bexp -> HoareTriple
hoareAssignment v e q =
  HoareTriple
  (substBexp q (aoptimize e) v)
  (CAssign v e)
  q
\end{lstlisting}

There are several ways how the function \texttt{substBexp} can be implemented. We list a few:

\begin{itemize}
\item One way is to do a full \texttt{Aexp}/\texttt{Bexp} evaluation; we can do this, since these languages are strongly normalizing, compared to our imperative language. However, this evaluation can still take some time, and it requires a context in addition.
\item Another way is to specify some concrete set of mathematical rewrites that can be applied, based on the original languages (\texttt{Aexp}/\texttt{Bexp}).
\end{itemize}

In this paper, the substitution will contain a concrete set of mathematical rewrites, though the reader is encouraged to try different implementations and spot the advantages and disadvantages of each.

\begin{lstlisting}
substAexp :: Aexp -> Aexp -> Char -> Aexp
substAexp (AId x) e v = if x == v then e else AId x
substAexp (APlus x y) e v = APlus (substAexp x e v) (substAexp y e v)
substAexp (AMinus x y) e v = AMinus (substAexp x e v) (substAexp y e v)
substAexp (AMult x y) e v = AMult (substAexp x e v) (substAexp y e v)
substAexp x _ _ = x

substBexp :: Bexp -> Aexp -> Char -> Bexp
substBexp q@(BEq x y) e v = BEq (aoptimize $ substAexp x e v) (aoptimize $ substAexp y e v)
substBexp q@(BLe x y) e v = BLe (aoptimize $ substAexp x e v) (aoptimize $ substAexp y e v)
substBexp (BAnd b1 b2) e v = BAnd (substBexp b1 e v) (substBexp b2 e v)
substBexp (BNot b) e v = BNot (substBexp b e v)
substBexp q _ _ = q
\end{lstlisting}

This implementation will rewrite a variable with an arithmetical expression within a boolean expression. We can now deduce some proofs, such as:

\begin{lstlisting}
> hoareAssignment 'X' (ANum 3) (BEq (AId 'X') (ANum 3))
{3 == 3} X := 3; {X == 3}
\end{lstlisting}

Given the precondition \texttt{3 = 3}, the command \texttt{X := 3} implies the postcondition \texttt{X = 3}.

\subsubsection{H-Consequence}

The consequence rule can be used to strengthen a precondition and/or weaken a postcondition in a Hoare triple.

Ideally, the implication in the consequence rule would represent the evaluation of a higher-order logic, but we're limiting it to optimization (our own small logic) for simplicity. That is, we use $\hookrightarrow$ in place of $\to$ in the rule.

In other words, the rule provides a way to transform a Hoare triple by embedding the result of an evaluation of a logic (in this case specifically, the optimization result of \texttt{boptimize}) into a Hoare triple.

\begin{lstlisting}
hoareConsequence :: Bexp -> HoareTriple -> Bexp -> Either String HoareTriple
hoareConsequence p1 (HoareTriple p2 c q2) q1
  | boptimize p1 == p2 && q1 == q2 = Right $ HoareTriple p1 c q1
  | q2 == boptimize q1 && p1 == p2 = Right $ HoareTriple p1 c q1
hoareConsequence _ _ _ = Left "Cannot construct proof"
\end{lstlisting}

For example, considering the \texttt{hoareAssignment} example we saw earlier, we can modify its precondition as follows:

\begin{lstlisting}
> hoareAssignment 'X' (ANum 3) (BEq (AId 'X') (ANum 3))
{3 == 3} X := 3; {X == 3}
> hoareConsequence (BAnd BTrue (BEq (ANum 3) (ANum 3))) (hoareAssignment 'X' (ANum 3) (BEq (AId 'X') (ANum 3))) (BEq (AId 'X') (ANum 3))
Right {TRUE && 3 == 3} X := 3; {X == 3}
\end{lstlisting}

\subsubsection{H-Sequence}

For the Hoare sequence rule, given two Hoare triples, the postcondition of the first triple must be equivalent to the precondition of the second triple (for some definition of equivalent).

\begin{lstlisting}
hoareSequence :: HoareTriple -> HoareTriple -> Either String HoareTriple
hoareSequence (HoareTriple p c1 q1) (HoareTriple q2 c2 r)
  | q1 == q2 = Right $ HoareTriple p (CSequence c1 c2) r
hoareSequence _ _ = Left "Cannot construct proof"
\end{lstlisting}

As with \texttt{eval}, the \texttt{Either} data type is used to distinguish between provable and not provable terms. Several commands can be chained as follows:

\begin{lstlisting}
> let c1 = hoareAssignment 'Y' (ANum 1) (BAnd (BEq (AId 'Y') (ANum 1)) (BEq (AId 'X') (AId 'X')))
{1 == 1 && X == X} Y := 1; {Y == 1 && X == X}
> let c2 = hoareAssignment 'Z' (AId 'X') (BAnd (BEq (AId 'Y') (ANum 1)) (BEq (AId 'Z') (AId 'X')))
{Y == 1 && X == X} Z := X; {Y == 1 && Z == X}
> hoareSequence c1 c2
Right {1 == 1 && X == X} Y := 1; Z := X; {Y == 1 && Z == X}
\end{lstlisting}

\subsubsection{H-Conditional}

Next, we provide an implementation for the Hoare conditional rule, covering both cases for commutativity of logical and.

\begin{lstlisting}
hoareConditional :: HoareTriple -> HoareTriple -> Either String HoareTriple
hoareConditional (HoareTriple (BAnd b1 p1) c1 q1) (HoareTriple (BAnd (BNot b2) p2) c2 q2)
  | b1 == b2 &&
    p1 == p2 &&
    q1 == q2 = Right $ HoareTriple p1 (CIfElse b1 c1 c2) q1
hoareConditional (HoareTriple (BAnd p1 b1) c1 q1) (HoareTriple (BAnd (BNot p2) b2) c2 q2)
  | b1 == b2 &&
    p1 == p2 &&
    q1 == q2 = Right $ HoareTriple p1 (CIfElse b1 c1 c2) q1
hoareConditional _ _ = Left "Cannot construct proof"
\end{lstlisting}

For the purposes of example, we start by considering the command:

\begin{lstlisting}
> CIfElse (BNot (BEq (AId 'X') (ANum 0))) CSkip (CAssign 'X' (APlus (AId 'X') (ANum 1)))
(If (! (X == 0)) Then (;) Else (X := X + 1;));
\end{lstlisting}

The conditionals $B$ and $P$ are given in the code. Now, following the rule to construct a Hoare triple for this command, we have to construct two proofs:

\begin{itemize}
\item $\{X \neq 0 \land P\} \ ; \{Q\}$
\item $\{\neg(X \neq 0) \land P\} X := X + 1; \{Q\}$
\end{itemize}

To be able to construct these proofs, we will add another substitution rule:

\begin{lstlisting}
substBexp q@(BEq (AId x) (ANum 0)) (APlus (AId x2) (ANum y1)) v
  | x == x2 && x2 == v && y1 > 0 = BNot (BEq (AId x) (ANum 0))
  | otherwise = q
\end{lstlisting}

This substitution rule states that whenever $x$ is zero, increasing it by a non-zero value will produce the postcondition that $x$ is no longer zero. We also expand \texttt{boptimize} to handle double negation properly:

\begin{lstlisting}
boptimize (BAnd (BNot (BNot b1)) b2) = BAnd b1 b2
\end{lstlisting}

With these additions, we can construct the two proofs as follows:

\begin{lstlisting}
> let eg1 = hoareSkip (BAnd (BNot (BEq (AId 'X') (ANum 0))) (BEq (ANum 0) (ANum 0)))
{! (X == 0) && 0 == 0} ; {! (X == 0) && 0 == 0}
> let eg2 = hoareAssignment 'X' (APlus (AId 'X') (ANum 1)) (BAnd (BNot (BEq (AId 'X') (ANum 0))) (BEq (ANum 0) (ANum 0)))
{! (! (X == 0)) && 0 == 0} X := X + 1; {! (X == 0) && 0 == 0}
\end{lstlisting}

At this point, we can apply the conditional rule:

\begin{lstlisting}
> hoareConditional eg1 eg21
Right {0 == 0} (If (! (X == 0)) Then (;) Else (X := X + 1;)); {! (X == 0) && 0 == 0}
\end{lstlisting}

\subsubsection{H-While}

In this part, we provide the implementation of the last and most important and complex rule.

\begin{lstlisting}
hoareWhile :: HoareTriple -> Either String HoareTriple
hoareWhile (HoareTriple (BAnd b p1) c p2)
  | p1 == p2  = Right $ HoareTriple p1 (CWhile b c) (BAnd (BNot b) p1)
hoareWhile (HoareTriple (BAnd p1 b) c p2)
  | p1 == p2  = Right $ HoareTriple p1 (CWhile b c) (BAnd (BNot b) p1)
hoareWhile _ = Left "Cannot construct proof"
\end{lstlisting}

The implementation for constructing the rule is straight-forward, but the tricky part is that we have to properly construct the precondition and the postcondition so that they match the rule.

Similarly as we did with the \texttt{H-Conditional} rule, for the purposes of example, we will consider the command:

\begin{lstlisting}
> CWhile BTrue CSkip
(While (TRUE) Do {;});
\end{lstlisting}

In this case, we get that $B$ is \texttt{BTrue} and $S$ is \texttt{CSkip}. Thus, we must provide the Hoare triple $\{\top \land P \} \ ; \{P\}$. We can use \texttt{hoareSkip} together with \texttt{hoareConsequence} to construct such triple as follows:

\begin{lstlisting}
> let c = hoareSkip (BEq (ANum 0) (ANum 0))
{0 == 0} ; {0 == 0}
> hoareConsequence (BAnd BTrue (BEq (ANum 0) (ANum 0))) c (BEq (ANum 0) (ANum 0))
Right {TRUE && 0 == 0} ; {0 == 0}
\end{lstlisting}

Now we can deduce the following:

\begin{lstlisting}
> whenRight (hoareConsequence (BAnd BTrue (BEq (ANum 0) (ANum 0))) c (BEq (ANum 0) (ANum 0))) (\x -> hoareWhile x)
Right {0 == 0} (While (TRUE) Do {;}); {! (TRUE) && 0 == 0}
\end{lstlisting}

That is, from an infinite loop ($\bottom$), follows anything (false).

\subsection{Example proof: Swap}

Even though the implementation we provide is simple enough, it can still be used to prove some useful facts:

\begin{lstlisting}
> hoareAssignment 'a' (APlus (AId 'a') (AId 'b')) (BAnd (BEq (AMinus (AId 'a') (AId 'b')) (AId 'A')) (BEq (AId 'b') (AId 'B')))
{(a + b - b) == A && b == B} a := a + b; {(a - b) == A && b == B}
> hoareAssignment 'b' (AMinus (AId 'a') (AId 'b')) (BAnd (BEq (AId 'b') (AId 'A')) (BEq (AMinus (AId 'a') (AId 'b')) (AId 'B')))
{(a - b) == A && (a - (a - b)) == B} b := (a - b); {b == A && (a - b) == B}
> hoareAssignment 'a' (AMinus (AId 'a') (AId 'b')) (BAnd (BEq (AId 'b') (AId 'A')) (BEq (AId 'a') (AId 'B')))
{b == A && (a - b) == B} a := (a - b); {b == A && a == B}
\end{lstlisting}

That is, we start with the precondition that \texttt{a == A \&\& b == B} and we reach \texttt{b == A \&\& a == B}. We proved that this set of commands will swap the values between two variables. To make this proof more obvious, we can add two additional optimization rules that state $a + b - b = a$ and $a - (a - b) = b$:

\begin{lstlisting}
aoptimize q@(AMinus (APlus (AId a1) (AId a2)) (AId a3))  = if a2 == a3 then AId a1 else q
aoptimize q@(AMinus (AId a1) (AMinus (AId a2) (AId a3))) = if a1 == a2 then AId a3 else q
\end{lstlisting}

The proof can be completed with \texttt{hoareSequence} as follows:

\begin{lstlisting}
> whenRight (hoareSequence swap1 swap2) (\x -> hoareSequence x swap3)
Right {a == A && b == B} a := a + b; b := (a - b); a := (a - b); {b == A && a == B}
\end{lstlisting}

\section{Conclusion}

Compile-time, run-time, etc. are all about having evaluations at different levels. There is still computation going on, but the computation strategies at the compile-time level may be different from those at the run-time level. A full evaluation of \texttt{Command} can be expensive, and sometimes even not terminate, and we wanted a way to deduce propositions without doing a full evaluation. We showed how to achieve this, by implementing a subset of Hoare logic as a proof of concept.

The evaluation strategies of a programming language directly affect how a programmer thinks. For example, if a programming language has a type system, the programmer will take advantage of it, subconsciously being aware of the (different) evaluation strategies for the type checker, in addition to the regular evaluation strategy for programs. If a programming language has the feature to verify properties about programs, such as Hoare logic, the programmer will be aware of this different evaluation strategy and use it to write more correct software.

Even though the mathematical formulas for Hoare logic look simple, implementing them is a different matter. The implementation details cover stuff like "what expressions do we want to support", "are we working with a strongly normalizing language", "what's the language that will represent propositions", etc. while these details are hidden in the mathematical representation of these formulas.

\section{Conflict of interest}

The author declares that they have no conflict of interest.

\begin{thebibliography}{1}

\bibitem{b1}
Rustan M. Leino
\newblock Dafny: An automatic program verifier for functional correctness
\newblock {\em International Conference on Logic for Programming Artificial Intelligence and Reasoning, pp. 348-370. Springer, Berlin, Heidelberg}, 2010.

\bibitem{b2}
Leonardo De Moura, Nikolaj Bjørner
\newblock Z3: An efficient SMT solver.
\newblock {\em International conference on Tools and Algorithms for the Construction and Analysis of Systems. Springer, Berlin, Heidelberg}, 2008.

\bibitem{b3}
Benjamin C. Pierce, Arthur Azevedo de Amorim, Chris Casinghino, Marco Gaboardi, Michael Greenberg, Cătălin Hriţcu, Vilhelm Sjöberg, Brent Yorgey
\newblock Logical Foundations
\newblock {\em Electronic textbook}, 2020.

\bibitem{b4}
Bruno Barras, Samuel Boutin, Cristina Cornes, Judicaël Courant, Jean-Christophe Filliâtre, et al.
\newblock The Coq Proof Assistant Reference Manual: Version 6.1
\newblock {\em [Research Report] RT-0203, INRIA}, 1997.

\bibitem{b5}
Boro Sitnikovski
\newblock Gentle Introduction to Dependent Types with Idris
\newblock {\em Leanpub/Amazon KDP}, 2018.

\bibitem{b6}
Miran Lipovaca
\newblock Learn You a Haskell For Great Good
\newblock {\em No Starch Press}, 2011.

\bibitem{b7}
Charles A.R. Hoare
\newblock An axiomatic basis for computer programming
\newblock {\em Communications of the ACM, 12(10), pp.576-580}, 1969.

\end{thebibliography}

\end{document}
